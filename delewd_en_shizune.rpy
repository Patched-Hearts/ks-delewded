# Closer (Shizune Act 3 Tied to Chair)
label en_delewd_S22c:
"The way she says it so factually stings me."

his "A lot of things make me nervous."

his "You're one of them."

show shizu behind_blank_cas_close
with charachange

shi "…?"

his "Because you're overeager to always get people involved in… whatever you're doing. Whether it's joining the Student Council, or even taking a break. Whether they want to or not."

show shizu basic_angry_cas_close
with charachange

shi "…"

show shizu adjust_happy_cas_close
with charachange

shi "… … … …"

show shizu basic_normal2_cas_close
with charachange

shi "… …"

"She signs almost at a crawl, her hands pausing mid-sentence far too much, causing the words to dissipate formlessly before I can even begin to try to understand them. I try not to let on that this is the case."

"It seems to work, but she looks a little sad, and I regret that I have nothing to say to snap her from the strangely wistful and distant expression she is wearing. All I can do is wait for her to come out of it."

show shizu behind_sad_cas_close
with charachange

ssh "You are right. I want to drag everyone into my life. But, lately, I'm no longer sure if it's the right thing to do."

his "I enjoyed you taking me to your favorite restaurant the other night."

show shizu basic_normal_cas_close
with charachange

ssh "It's not like that was my favorite restaurant… I have others I like. I might even be able to rank them by number."

his "Really…"

show shizu adjust_frown_cas_close
with charachange

ssh "This chair is so hard. I want to sit on the bed."

"Motioning to her to go ahead, I wait for her to get off the chair and take her place when she does. Though I didn't intend for it to be, she finds it amusing."

show shizu behind_smile_cas_close
with charachange

stop music fadeout 5.0

ssh "Close your eyes."

his "Why?"

show shizu adjust_smug_cas_close
with charachange

ssh "It's a surprise."

show black
with shuteye

"I decide to humor her and close them. I can feel her leaning over me, and suddenly, something soft and moist touches my lips. My body tenses up in surprise. Fortunately, not as awkward a reaction as I could have made."

"It was just a quick peck, and I almost think that's the end of it, but then she kisses me again, more deeply this time. Her hands slide onto my shoulders, up to my neck, and then back down again. Then across my shoulders and down my arms."

"I can feel the weight of her body on my legs. At this point, I'm ready to try and open my eyes just a crack, but as if expecting it, she puts her fingers on my eyelids."

play sound sfx_rustling

"Seconds later, something ties my hands together at the wrists, and I panic, not knowing what to make of this. My first thought is to ask Shizune what she's thinking. Even though she can't hear me, I'm sure she gets the gist of it."

"She won't let go of my hands, tracing her fingers over them, from the lines of my palms, over my knuckles, and to my wrists."

scene evh shizune_hcg_tied_stare:
    yalign 0.0 xalign 1.0 subpixel True
    easein 6.0 xalign 0.5 zoom 0.5
    truecenter
    zoom 1.0
    "evh shizune_hcg_tied_stare_small"
with whiteout

play music music_heart fadein 5.0

hi "Hey, what are you doing? What's this?"

"Or course, with my hands tied behind my back, I might as well be gagged. A part of me can't help but think that this is what she intended."

scene evh shizune_hcg_tied_smile_small
with charachange

"As if reading my thoughts, a mischievous expression lights up her face, but her blushing doesn't fade. In fact, it only deepens when our eyes meet."

"Embarrassed, she leans deeper into our partial embrace, hiding her face by burying it in my shoulder and neck. Her hair is soft and tickles me, and I let out a laugh knowing that she won't hear me; won't be offended."

label en_delewd_S22h:

scene evh shizune_hcg_tied_blush_small
with charachange

"Shizune almost falls off me from nervousness."

"The sudden display of naivety is the starkest contrast yet to how forward she has been so far, and I find it amusing. Suddenly, she seems very immature again. A high-school girl playing the role of a more aggressive woman."

scene evh shizune_hcg_tied_stare:
    yalign 0.0 xalign 0.8
show evh_hi shizune_hcg_tied_hisao2:
    yalign 0.0 xalign 0.8
# hide evh_hi
with charachange

"It's likely Shizune is as nervous as I am, but then I think about what's next to come."

"She might try and unbutton my shirt. Who knows what she would say, seeing the scar on my chest. I'm still self-conscious about it, and I can imagine the concern on her face on seeing it; the tenting of her fingers in thought."


"Luckily, in this position, she couldn't take off my sweater without ripping it off me. The fear fades from my mind. Now, I'm only experiencing a strange, uncomfortable mix of anticipation and nervousness."

show evh shizune_hcg_tied_blush
with charachange

"I wonder exactly when it was that I started being attracted to her. Not just attracted to her physically, but drawn to her. And, I wonder why. She's pretty, but then, also very combative. Not just that, but she seems to like being that way."

scene evh shizune_hcg_tied_blush_small
with charachange

"The way she's acting now, however, and at other times, doesn't really fit that image. I'm starting to think that maybe her tying my hands might have been for more reasons than just the most obvious."

"Still, that aggressiveness that she flashes around as comfortably as a business card is real. I don't know whether or not that kind of attitude could be considered dangerous. If it is, I wonder what kind of person that makes me."

hi "It was probably the first week I was here. A week doesn't sound so long when I think about it, but at the time it did. Even though I pretty much thought my days were numbered that week, it still seemed to go by so slowly."

"Even if she can't hear me, it puts me at ease."

hi "I started to realize that I didn't have that much to complain about. But there's still…"

hi "Well, never mind."

scene evh shizune_hcg_tied_stare_small
with charachange

"She glances at me, for no reason other than that I'm talking. Because she can't understand what I'm saying, Shizune becomes increasingly flustered, but doesn't sign anything in reply."

scene evh shizune_hcg_tied_close:
    yalign 0.1 xalign 0.8
show evh_hi shizune_hcg_tied_hisao2:
    yalign 0.1 xalign 0.8
with Dissolve(2.0)

"I attempt to kiss her, but only manage to succeed in touching our foreheads together, at least not painfully."

"My thoughts wander briefly to whether or not the door is locked. If it were to open now, I'd probably have a heart attack, literally. And then there's the question of who would be opening the door."

label en_delewd_S22x:

hide evh_hi
with charachange

"Her glasses are slightly askew, and this is the first time she isn't messing around with them in some way. I want to straighten them for her, but the second I try, I'm reminded that I can't. Shizune seems to have forgotten it as well."

stop music fadeout 7.0

scene evh shizune_hcg_tied_blush_small:
    truecenter
    subpixel True zoom 1.2
    easein 10.0 zoom 1.0
with Dissolve(2.0)

"Instead of getting up, she presses herself against me in the chair to extend her reach. It's almost as if this is the only position she can think to untie my hands from. That is what I think as I feel her unbinding my wrists."

"However, she doesn't get off me. Her fingers gently stroke against mine, occasionally bending inwards to run over my palms. It's funny, but I feel more connected to Shizune through this simple act than before."

"Shizune stays pressed against me like this for some time. It's a little uncomfortable, but it makes me feel happy, as if I could stay like this for hours."

scene black
with dissolve

# Tongue-Tied (Shizune Act 3 Comfort Misha)
label en_delewd_S28a:
play music music_moonlight fadein 4.0

"As much as I pretend to protest, I've allowed things to come to this point. Even though I knew so far ahead of when she actually came out with it that this was what she was getting at."

"At the very least, I was okay with this outcome. If I needed any more proof, it's simple: I still haven't turned her away."

"I could have at any point, and it was wrong of me not to do it sooner, but now, not doing so is something beyond simple carelessness."

show mishashort perky_sad_close_ss:
    linear 0.2 alpha 0 ypos 1.1
with vpunch

"Misha takes my silence as agreement, and squeezes herself against me tightly, as if she is trying to get into my clothes. My arms are enveloped by the softness of her skin and the warmth of her body. I roll over on reflex, and end up on top of her."

hide mishashort
with None

"She looks at me, as if expecting me to take the lead, and closes her eyes nervously the second I return her gaze."

label en_delewd_S28h:

"Her figure is curvier than I expected, and complements her cute face."

"Her eyes are open now, and I run my hand up the inside of her thigh, almost laughing when she shudders and nearly crushes it when she tightens her legs. A genuine reaction, and a cute one."

"Shizune was better at hiding her inexperience, even though she was just as embarrassed."

"Grabbing at my shoulders, she pulls herself against me, joining us more tightly together."

"Her hands grip each other around my back, and I feel her elbows digging into the space under my ribs."

mi "Hnn~…"

"Her voice is so quiet and breathy that I have trouble understanding her, but the tone of sadness permeating it is unmistakable, and only makes me feel guiltier."

"I'm supposed to be comforting her, but it's entirely physical, and if Misha is being reassured in any way by this, she isn't showing it. That makes me question whether my decision was the right one. I'm really starting to doubt it."

"Despite that, her soft cooing in my ear makes me keep going. Her leg twisting around mine, she tenses up, her smooth neck rubbing across my cheek."

stop music fadeout 6.0

scene bg school_dormhisao_ss
with shorttimeskip

"It takes her a minute to separate herself from me. I feel cold."

mi "…Hicchan?"

"I can't hear anything but the ticking of the clock, and the sound of my own breathing."

mi "…Never mind, Hicchan."

"I search around for Misha's hand with my own, and caress it. How light and delicate it seems, even as it grabs me so tightly around my wrist. The feeling is familiar."

scene black
with dissolve


# Succession (Shizune Act 4 Student Council Room)
label en_delewd_S35:
scene bg school_council_ss at right
show mishashort hips_smile_close_ss at closeleft
show shizu behind_blank_close_ss at closeright
with locationchange

play music music_ease

hi "…And remember, you have to take this job seriously. Too many people think you can just slack off, and that it isn't important. That is a dangerous way of thinking."

show mishashort cross_frown_close_ss
with charachange

mi "Definitely~. You can't take it too seriously~! If you aren't always thinking big, thinking positive, and if you show any signs of weakness, people will start to think you're incompetent, you know~."

show mishashort sign_confused_close_ss
with charachange

mi "And soon you won't be able to do anything because your power is going to be delegated off to others piece by piece, and you'll be left with nothing. That's what happened last time~."

show mishashort hips_grin_close_ss
with charachange

mi "So~! Remember~, it may seem like an easy job, but a lot of carnage can happen in this room. Ahaha~. And~, out of it. Dealing with school staff, too! Even trying to get a budget report from a class rep can be a fight to the death~, sometimes."

hi "…Yeah. It's kill or be killed. There are no friends in the pits and you take no prisoners. …Are you sure about this? Is this right?"

show shizu basic_angry_close_ss
with charachange

ssh "You don't seem excited enough, I have to make sure it's getting through properly. Once more, with feeling!"

show aoi_keiko:
    yalign 1.0 xanchor 0.5 xpos 0.0 alpha 0.0
with None

play sound sfx_flash

show bg school_council_ss at left
show mishashort invis_close at Position(xpos=1.1)
show shizu invis_close at Position(xpos=1.6)
show aoi_keiko:
    center
    alpha 1.0
with Dissolvemove(0.5)

"Shizune twists her hands like a maestro for emphasis, visibly intimidating the two girls standing at attention in front of us. To think this all started because one of them asked if she wasn't taking her job too seriously."

ssh "Do you understand!?"

hi "Do you understand? Pretend I'm shouting it."

"Aoi" "Okay, okay! Aaargh! This Student Council is so weird."

"Keiko" "Yes, sir."

hi "'Sir?' Who are you guys talking to, anyway?"

play sound sfx_flash

show bg school_council_ss at right
show mishashort hips_smile_close_ss at closeleft
show shizu adjust_frown_close_ss at closeright
show aoi_keiko:
    yalign 1.0 xanchor 0.5 xpos 0.0 alpha 0.0
with Dissolvemove(0.5)

ssh "It's not weird! You have to think of it as a job. If you want, think of it like they are paying you with the right to use this great office."

play sound sfx_flash

show bg school_council_ss at left
show mishashort invis_close at Position(xpos=1.1)
show shizu invis_close at Position(xpos=1.6)
show aoi_keiko:
    center
    alpha 1.0
with Dissolvemove(0.5)

hi "You want another lecture?"

"Aoi" "Noooo…"

ssh "You can go now."

stop music fadeout 5.0

scene bg school_council_ss
show mishashort perky_smile_ss:
    twoleft
    ypos 1.1
with shorttimeskip

"Just like that, the hour-long student council orientation is over. Personally, I thought it was about fifty minutes too long, and also found it funny that it incorporated a tour of a school that we have all been going to for a while, but I guess it didn't hurt."

"I expect Shizune to fall back into her chair, since she has been on edge all day, but she doesn't. She continues pacing the room restlessly."

show shizu invis:
    center
    xpos 1.0
with None

play music music_shizune fadein 1.0

show shizu adjust_frown_ss at tworight
with dissolvecharamove

ssh "They still have a long way to go! Right now, they're a joke."

show mishashort sign_confused_ss:
    twoleft
    ypos 1.1
with charachange

mi "Eh?"

hi "What?"

show shizu behind_frustrated_ss
with charachange

ssh "They think they can be the new Student Council? They're so unfocused. You can really see the lack of experience. This was our best year yet; I don't think they have what it takes to be our follow-up act."

show shizu basic_frown_ss
with charachange

ssh "And I know there are more of them than those two girls. Where are they? They're like the heavily-marketed but mediocre, big-budget, critically-panned sequel to the acclaimed, low-budget sleeper hit."

show mishashort perky_confused_ss
show shizu behind_blank_ss:
    ypos 1.1
with dissolvecharamove

"Eventually, she does stop and sit down."

hi "Are you going to miss it?"

show shizu basic_normal_ss
with charachange

ssh "Obviously."

show mishashort perky_sad_ss
with charachange

mi "Hm~… I'd be happier if I didn't have to leave, too."

show mishashort hips_smile_ss
with charachange

mi "I like being in the Student Council, even if it can be tiring, too."

hi "Yeah, it's definitely tiring."

show mishashort hips_grin_ss
with charachange

mi "Only because Shicchan is always trying to do more than she has to~."

show shizu adjust_frown_ss
with charachange

ssh "You're forgetting that if I did the bare minimum, we wouldn't do anything all year except hand out flyers, collect surveys, and plan the next student council election so the next Student Council could sit around for another year of doing nothing."

show shizu behind_frown_ss
with charachange

ssh "Asking me to let that happen? Don't be ridiculous. In a Student Council like that there wouldn't even be any power to play around with."

show shizu adjust_happy_ss
with charachange

ssh "I'm just happy that even though I clearly need to ride them harder, those two aren't bad. Not there yet, but the new Student Council should be in good hands."

hi "How can you tell?"

show shizu behind_smile_ss
with charachange

ssh "After the festival, they asked me if we could also organize a Halloween event, like a haunted house or something along those lines. They had a bunch of other ideas, as well."

show shizu adjust_smug_ss
with charachange

ssh "Of course my response was 'no.' I had Misha tell them to do it themselves, if they wanted it so badly. They were angry, for some reason."

show mishashort cross_laugh_ss
with charachange

mi "Ahaha~."

hi "Of course they'd be angry if you said that."

"And Misha delivering the message wouldn't help."

show mishashort cross_smile_ss
show shizu behind_blank_ss
with charachange

ssh "I was angry too."

show shizu basic_frown_ss
with charachange

ssh "All of a sudden, they want so much. If they wanted a haunted house, or a traditional-style café, or a trip to the beach, or whatever other cliché thing, why didn't they try to organize it before? It was like they were taking advantage of me."

show shizu behind_frown_ss
with charachange

ssh "I worked hard to organize those festivals, and in return they came to me with 'That was nice, but can you do this now? How about doing this? It's what I really want.'"

show mishashort sign_smile_ss
with charachange

mi "Shicchan was wrong~, though."

show shizu basic_happy_ss
with charachange

ssh "Right. They wanted to join the Student Council so they could make it happen. I made them feel jealous and riled them up. That can be a kind of motivation too."

show shizu adjust_happy_ss
with charachange

ssh "The desire to do something great spreads, even if it's to show me up. They decided to take me up on the challenge nonetheless."

show shizu behind_blank_ss
with charachange

ssh "I'm impressed. Well, for now. I would have to see how it plays out a little longer in order to know for sure."

play sound sfx_snap

show shizu adjust_happy_ss
show mishashort perky_confused_ss:
    ease 0.1 ypos 1.05
    ease 0.1 ypos 1.1
with vpunch

"She snaps her fingers suddenly, which sends Misha almost bolting out of her seat. Interesting, I guess it is impossible to get used to."

show shizu basic_happy_ss
with charachange

ssh "That's right! We were going to have a party to celebrate passing the reins to the new Student Council, weren't we? Why not have that now? Or at least plan it now, and have it tomorrow."

hi "But they're not even in charge yet. In fact, that's the first thing you told them: 'You're not in charge yet.' It seems premature."

show shizu adjust_frown_ss
with charachange

shi "…"

show shizu behind_blank_ss
with charachange

ssh "Misha, what do you think?"

show mishashort hips_smile_ss
with charachange

mi "Hmmm~, I agree, it's too early. Plus~, I don't think I could go anyway. Sorry~! In fact, I was going to leave right now."

ssh "Why not?"

show mishashort hips_grin_ss
with charachange

mi "No~ comment~!"

show shizu adjust_frown_ss
with charachange

ssh "Come on, tell me."

show mishashort perky_confused_ss
with charachange

mi "Well… okay~!"

"Way to not crack under pressure, Misha."

show mishashort sign_confused_ss
with charachange

mi "I thought about it, and~… Even if I didn't want to go, I would say yes~! Usually~. It's the kind of person I am. I really should stop doing that, and this is a good place to start, I think."

show mishashort perky_sad_ss
with charachange

mi "If it's a celebration to say goodbye, I don't want it. It would be too sad~. I want to do something else instead. And after all, Hicchan, you and Shicchan will still be here tomorrow. It doesn't seem right."

show mishashort hips_grin_ss
with charachange

mi "Besides, I have other school things I have to do today~! I can't drop them just like that."

show shizu adjust_frown_ss
with charachange

ssh "We can postpone it."

show mishashort hips_frown_ss
with charachange

mi "No. No early goodbyes~!"

"She looks very firm as she says this."

hi "Aren't you going to go now, though?"

show mishashort hips_grin_ss
with charachange

mi "Hm~? Oh, that's right~! Wahaha~!"

show mishashort perky_smile_ss at twoleft
with Dissolvemove(0.7)

show mishashort sign_smile_ss
with charachange

mi "Okay, besides now, no too-early goodbyes, okay?"

show shizu behind_blank_ss
with charachange

ssh "I get it."

show mishashort hips_grin_ss
with charachange

mi "Okay, later~!"

stop music fadeout 4.0

hide mishashort
with charaexit

show bg school_council_ss:
    subpixel True
    center
    parallel:
        "bg school_council_ni" with Dissolve(5.0)
    parallel:
        ease 5.0 bgleft
show shizu behind_blank_ss:
    subpixel True
    parallel:
        "shizu behind_blank" with Dissolve(5.0, alpha=True)
    parallel:
        ease 5.0 xpos 0.5
with None

"With that, it's just Shizune and me left alone in the student council room."

play music music_dreamy fadein 4.0

with Pause(2.0)

"Sunset slowly changes to night as we sit in silence, both searching for something to say."

show bg school_council_ni at bgleft
show shizu adjust_frown:
    center
    subpixel False ypos 1.1
with Dissolvemove(0.5)

ssh "Would it really be that bad?"

his "Yeah. I didn't think about it like that, but Misha's right. Parties set a mood, and it would be a sad one. A sad party doesn't sound like a whole lot of fun."

show shizu basic_angry
with charachange

ssh "Why would it be sad?"

"Is it a trick question? I'm sure of it. Shizune's eyes pierce into mine, waiting for my answer with a detached, analytical stare that I haven't seen in a while, but feels familiar anyway."

"I consider my answer carefully, but also what it means for her to ask me."

"It could be that Shizune finds it depressing as well. Or it could be that she doesn't understand why anyone would find it depressing. Both are equally plausible."

his "I had a thought that when you graduate, that's it. It's going to be the end of the Student Council. I was wondering if you had the same idea."

show shizu behind_frown
with charachange

ssh "Don't be stupid. I look forward to it. I won't be a student any more, so the expectations are going to be completely different. People's expectations of me, and my expectations about everything else. It seems exciting!"

show shizu adjust_frown
with charachange

ssh "As for the Student Council, it should be in good enough hands. I don't have anything to be sad about."

his "I don't think you're being honest. You looked upset about having to give the Student Council up not even a few weeks ago. It wasn't about leaving it to a bunch of newbies either, it was having to stop doing student council work at all."

show shizu behind_smile
with charachange

"Unexpectedly, Shizune smiles."

his "So, you're not disagreeing."

his "Then it doesn't make sense. Why would you want to have a party about it?"

show shizu basic_normal
with charachange

ssh "I'm trying to get over it. Besides… Goodbye celebrations are very important. People say the first step is the most crucial, but following it through and finishing cleanly are just as important, right?"

his "I guess that is true."

show shizu adjust_smug
with charachange

ssh "Anyway, I don't consider it goodbye. But it's still an event. You still have to go through the proper motions."

show shizu behind_blank
with charachange

stop music fadeout 4.0

ssh "Aren't you going to?"

his "Aren't I going to what?"

show shizu basic_normal
with charachange

ssh "Kiss me, of course."

his "Is that 'the proper motions?'"

show shizu behind_blank
with charachange

ssh "It would be normal, wouldn't it? The natural thing to do."

"It's time to act decisively. If I don't, I'm sure my heart will explode."

show shizu adjust_blush_close
with charachange

"I kiss her immediately, so quickly that I don't even have time to enjoy it. Even though she was prepared for it, Shizune blushes a deep red. I feel a similar heat rising in my neck and cheeks."

play music music_one fadein 4.0

scene evh shizu_undressing_clothed_stare
with whiteout

"I move in for another kiss, but as I do so, she moves backwards at the same time and impishly jumps onto the cabinet behind her. Alone, in the total silence of the room, we just look at each other for a while."

show evh shizu_undressing_clothed_kiss
with charachange

"This time, I kiss her more deeply. Her lips are light and dry, and open a tiny bit. I'm only able to appreciate the sensation for a moment before Shizune starts kissing me back forcefully."

"Her bangs brush against my closed eyelids as I let myself sink deeper into the kiss. I can feel the shape of her body through her clothes, which only makes me hold Shizune tighter."

show evh shizu_undressing_clothed_blush
with charachange

"It takes some effort for the both of us to draw back from each other. We're both blushing."

show evh shizu_undressing_clothed_kiss
with charachange

"Shizune surprises me by quickly pulling me in for a deep kiss without warning, but I quickly return the gesture."

show evh shizu_undressing_clothed_blush
with charachange

ssh "Why are you bolder today than on the roof?"

ssh "Or in your room?"

"I try to think of a good answer, but it isn't easy. How would I be able to respond to that even if I could? There's no way to, unless I were to say that bureaucracy really puts me in the mood."

scene bg school_council_ni
with locationchange
with vpunch

"I move in for another kiss and begin to slide my hand up her side, but she suddenly lurches forwards, making me stumble backward."

"The stiff edge of the table behind me was the furthest thing from my mind, until I feel it stabbing into my lower back. I hadn't even noticed it was there. It makes me grab Shizune a little tighter as we fall back onto the surface of the table."

label en_delewd_S35h:

"I hold back a sigh as Shizune victoriously holds herself above me. She's won again."

"I end up laughing, despite how hard I try not to, and it's contagious enough that Shizune starts to as well."

"The table under us rattles under our combined weight. I doubt it would collapse, but the noise is really something."

"Suddenly, she stops, almost falling onto me with enough speed that if she didn't catch herself, it would probably have knocked us unconscious. The worst situation possible, if someone happened to walk in while we were knocked out."

"Shizune lets out her breath sheepishly, realizing it at the same time as I do."

show shizu behind_blank
with charaenter

ssh "Sorry, I tripped, or slipped, or something like that."

his "I had a thought, is the door unlocked?"

hide shizu
with charaexit

"She quickly gets off the table and bolts over to check, and locks it, unlocks it, and locks it again, pulling on the knob just to make sure. When she's finally sure, she makes an out-of-place motion with her hands."

show shizu behind_smile
with charaenter

ssh "Safe!"

his "I'm glad you can take things so lightly."

show shizu behind_frown
with charachange

ssh "I didn't do it on purpose. Why don't you take the lead, then?"

show shizu behind_smile
with charachange

ssh "Come on."

hide shizu
with charaexit

"I grab Shizune by the shoulders and try to put her onto the table instead. Her brow scrunches in displeasure as the edge of the table pokes her in the back, just as it did to me. She opts to help herself up onto it."

"I run my hands along the curve of her body, from her shoulders on down."

"She squirms under me and twists her arms around mine instead, gripping my fingers and drawing me closer. It feels like I'm wrestling her; the lock is inescapable."

"From the first time our hands met, I guess we were connected."

"Whether it's through her pulling me from one student council event to another, or holding hands as lovers, I think it's been the same, the confidence that comes across in the way she grasps my hand."

stop music fadeout 4.0

"For Shizune's part, she smiles almost dreamily."

"The sight makes me smile as well."

label en_delewd_S35x:
scene bg school_council_ni
with shorttimeskip

show shizu behind_smile
with charaenter

ssh "It was a lucky break that Misha couldn't be here, wasn't it?"

his "You're in an unusually joking mood today."

his "I wonder what she had to do."

show shizu behind_blank
with charachange

"Shizune traces the air lazily with a finger and points to the door."

ssh "Go see for yourself."

his "Why don't you just tell me?"

show shizu behind_smile
with charachange

ssh "It's more interesting if you see for yourself. Seeing is believing."

his "Sure. Clever. Maybe I will. What about you, are you going to stay here all day? It's getting late."

show shizu behind_blank
with charachange

ssh "It feels like my last day as Student Council president, so maybe I'll sleep here tonight. It could be the last chance I have to sleep at my desk, like after a long day trying to meet a deadline."

his "That's weird."

his "I'll sleep in my bed."

ssh "Sleeping sitting up is a skill. A very useful one."

his "Right."

scene bg school_lobby_ni
with locationchange

"For a moment after I leave the room, I actually do consider seeing what Misha is up to, just because Shizune made it sound so secretive, as if she were building a time machine or something. But in the end I decide not to."

return